# 事件 
from ./lib/event import event_template

function ./game/event/1_night_fall:
     event_template("夜晚降临","black","黑暗里有点东西")
     time set night
     execute at @r[tag=ingame,tag=!out] run summon phantom 0 70 0
     execute at @r[tag=ingame,tag=!out] run summon phantom 0 70 0
function ./game/event/2_falling_anvil:
     event_template("铁砧下落","#5f665d","注意头顶！")
     execute as @a[tag=ingame,tag=!out] at @s run fill ~ ~1 ~ ~ ~5 ~ air
     execute as @a[tag=ingame,tag=!out] at @s run setblock ~ ~5 ~ anvil
function ./game/event/3_dragon:
     event_template("龙袭","red","要结束了？")
     execute at @r[tag=ingame,tag=!out] run summon ender_dragon 0 100 0 {Tags:["event.3_dragon"]}
     data modify entity @e[type=ender_dragon,limit=1] DragonPhase set value 8
function ./game/event/4_wither:
     event_template("凋灵","red","\u00a7kxxxxxx")
     execute at @r[tag=ingame,tag=!out] run summon wither 0 70 0 {Tags:["event.4_wither"]}
function ./game/event/5_fly:
     event_template("FLY","blue","\u00a7r\u00a7b自由的风")
     give @a[tag=ingame,tag=!out] elytra
     give @a[tag=ingame,tag=!out] firework_rocket 3
function ./game/event/6_rain/tip:
     event_template("箭雨","dark_purple","什么都有？(10s)")
     scoreboard players set #Timer EventDura.Sec 10
     execute as @a[tag=ingame,tag=!out] at @s run function ./game/event/6_rain/run
function ./game/event/7_sky_waker/tip:
     event_template("踏空","aqua","\u00a7r\u00a74恐怖如斯")
function ./game/event/8_rotation/tip:
     event_template("自转","#1de6f0","oiiaioooooiai(10s)")
     scoreboard players set #Timer EventDura.Tick 200
function ./game/event/9_lightning:
     event_template("雷击","gold","我的避雷针呢？")
     execute as @a[tag=ingame,tag=!out] at @s run summon lightning_bolt ~ ~ ~
function ./game/event/10_hello_world:
     event_template("你好,世界","green","Hello World!")
     time set day
     execute as @a[tag=ingame,tag=!out] run effect give @s instant_health 1 5
function ./game/event/11_undye:
     event_template("「不死」","gold","+1 life")
     execute as @a[tag=ingame,tag=!out] run give @s totem_of_undying 1
function ./game/event/12_broken_leg:
     event_template("断腿","red","")
     execute as @a[tag=ingame,tag=!out] run attribute @s jump_strength modifier add yw-pillar:event.broken_leg -0.5 add_value
function ./game/event/13_punch:
     execute if score #Game RuleID matches 3 run return run function yw-pillar:game/event/main
     event_template("一击必杀","yellow","ONE PUNCH!!!")
     execute as @a[tag=ingame,tag=!out] run attribute @s attack_damage base set 40
function ./game/event/14_creeper:
     event_template("CREEPER","white","Creeper?")
     execute as @a[tag=ingame,tag=!out] at @s run summon creeper ~ ~0.5 ~
     execute as @a[tag=ingame,tag=!out] as @s if predicate {condition:"random_chance",chance:0.1} run return run data modify entity @e[type=creeper,distance=..1,limit=1] powered set value true
function ./game/event/15_touch:
     event_template("摸摸","#e315c8","摸得更远")
     execute as @a[tag=ingame,tag=!out] run attribute @s entity_interaction_range modifier add yw-pillar:event.pat_pat 10 add_value
function ./game/event/16_inv_exchange:
     event_template("背包交换","green","这何尝不是一种NTR")
     function yw-pillar:game/event/16_inv_exchange/run
function ./game/event/17_king_game:
     event_template("国王游戏","gold","成王败寇")
     execute as @r[tag=ingame,tag=!out] run tag @s add event.17_king_game
     effect give @a[tag=event.17_king_game] glowing infinite 0
     item replace entity @a[tag=event.17_king_game] armor.head with stick[custom_name={text:"伪·王冠",color:"gray",bold:true,italic:false},lore=[{text:"『欲戴王冠 必承其重』",color:"red",underlined:true}],equippable={slot:"head"},enchantments={binding_curse:1},tooltip_display={hidden_components:["enchantments"]},item_model="yw-pillar:event/crown",attribute_modifiers=[{id:"yw-pillar:fake_crown_1",type:"armor",amount:2,operation:"add_value",slot:"head"},{id:"yw-pillar:fake_crown_2",type:"armor_toughness",amount:2,operation:"add_value",slot:"head"},{"id":"yw-pillar:fake_crown_3",type:"max_health",amount:5,operation:"add_value",slot:"head"}]]
     give @a[tag=ingame,tag=!out,tag=!event.17_king_game] stone 16
function ./game/event/18_lucky_doll:
     event_template("幸运玩偶","gold","qvq")
     execute as @a[tag=ingame,tag=!out] as @s run loot give @s loot yw-pillar:doll
function ./game/event/19_hungry:
     event_template("饿啊饿啊","#587653","eaea")
     execute as @a[tag=ingame,tag=!out] effect give @s hunger 30 40
function ./game/event/20_black:
     event_template("黑","white","真的黑")
     title @a times 10t 9s 10t
     title @a[tag=ingame,tag=!out] title "\uE005"
function ./game/event/21_speed:
     event_template("Speed","aqua","\u00a7b♿冲刺冲刺♿")
     execute as @a[tag=ingame,tag=!out] run attribute @s movement_speed modifier add yw-pillar:event.speed 0.2 add_value
function ./game/event/22_mini:
     event_template("迷你化","#ff023d","> <")
     execute as @a[tag=ingame,tag=!out] run attribute @s scale modifier add yw-pillar:event.mini -0.8 add_value
function ./game/event/23_huge:
     event_template("巨大化","#2403ff","= =")
     execute as @a[tag=ingame,tag=!out] run attribute @s scale modifier add yw-pillar:event.huge 4 add_value
function ./game/event/24_nuclear:
     event_template("核电","#06c103","核电，轻而易举")
     execute as @a[tag=ingame,tag=!out] at @s run summon creeper ~ ~ ~ {CustomName:{text:"坏了坏了",color:"red",bold:true},powered:true,ExplosionRadius:10}
     execute as @a[tag=ingame,tag=!out] run advancement grant @s only yw-pillar:main/event/24_nuclear
function ./game/event/25_rainy_day:
     event_template("雨天","aqua","我的世界下雨了☂")
     weather rain 24000
     give @a[tag=ingame,tag=!out] trident[enchantments={riptide:3}]
     advancement grant @a[tag=ingame,tag=!out] only yw-pillar:main/event/25_rainy_day 
function ./game/event/26_greedy_slime:
     event_template("贪吃的史莱姆","green","嚼嚼嚼~")
     scoreboard players set $SlimeSize math 0
     execute at @r[tag=ingame,tag=!out] run summon slime 0 66 0 {CustomName:{text:"大嘴",color:"red",bold:true},Tags:["event.26_greedy_slime"],Size:1,Glowing:true,attributes:[{id:"max_health",base:10}],active_effects:[{id:"slow_falling",duration:600}]}
function ./game/event/27_location_exchange:
     event_template("位置交换","yellow","给我干哪来了")
     function yw-pillar:game/event/27_location_exchange/run

# 规则
from ./lib/rule import rule_template

function ./game/rule/1_small_cute:
     rule_template("小小的也很可爱❤","#f90fce",-7273689,"玩家尺寸缩小为原来的\u00a7l1/3")
     execute as @a[tag=ingame,tag=!out] run attribute @s scale base set 0.33
function ./game/rule/2_big:
     rule_template("大！大！大！","gold",-38896125,"玩家尺寸增大为原来的\u00a7l3/2")
     execute as @a[tag=ingame,tag=!out] run attribute @s scale base set 1.5
function ./game/rule/3_my_partner/tip:
     rule_template("我的伙伴","blue",-48357795,"获得一只\u00a7l狐狸\n狐狸存活时,给玩家提供力量和生命回复")
     execute as @a[tag=ingame,tag=!out] run function ./utils/rule/fox_give with entity @s
function ./game/rule/4_punch:
     rule_template("一击必杀！","red",-41089492,"玩家攻击伤害变成\u00a7l40")
     execute as @a[tag=ingame,tag=!out] run attribute @s attack_damage base set 40
function ./game/rule/5_inv_exchange:
     rule_template("背包交换","green",-11832829,"随机事件固定为\u00a7l背包交换")
function ./game/rule/6_i_sea_you:
     rule_template("I sea you","white",-6287344,"玩家获得\u00a7r\u00a73\u00a7l隐身\u00a7r,事件结束后获得\u00a7r\u00a7l5s\u00a7r\u00a7f\u00a7l发光")
function ./game/rule/7_void_mercy:
     rule_template("虚空的仁慈","dark_purple",-11250604,"掉落虚空将被向上传送\u00a7r\u00a7l60\u00a7r格")

# 柱 
from ./lib/pillar import pillar_tempalte

pillar_tempalte("wool","bedrock")
pillar_tempalte("nether","red_nether_bricks")
pillar_tempalte("glass","glass")
pillar_tempalte("void","stone")
pillar_tempalte("moon","end_stone")
